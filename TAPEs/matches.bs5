10   REM *** GAME OF 23 MATCHES
20   PRINT "THIS IS THE GAME OF 23 MATCHES."
30   PRINT "  WE START WITH A PILE OF 23 MATCHES."
40   PRINT "  WE WILL TAKE TURNS REMOVING 1, 2 OR 3 MACTHES"
50   PRINT "  EACH TURN.  WHOEVER HAS TO TAKE THE LAST MATCH"
60   PRINT "  FROM THE PILE LOSES.  I'LL LET YOU GO FIRST."
70   PRINT "    GOOD LUCK!"
100  M=23: PRINT : PRINT : PRINT "THERE ARE NOW 23 MATCHES IN THE PILE"
120  INPUT "HOW MANY MATCHES DO YOU TAKE?"H
125  H=INT(H)
130  IF H<1 THEN 135
132  IF H<4 THEN 140
135  PRINT "NOW REALLY!! BE SERIOUS.  ";: GOTO 120
140  IF H>M THEN 135
145  M=M-H
150  IF M<=0 THEN PRINT "I WIN!!!!!!": GOTO 900
160  IF M=1 THEN PRINT "YOU WIN, LUCKY I GUESS.": GOTO 900
170  R=M-4*INT(M/4)
180  IF R=1 THEN C=INT(3*RND(0))+1: GOTO 200
190  C=(R+3)-4*INT((R+3)/4)
200  M=M-C: PRINT "I TOOK";C;"  ";
202  IF M=1 THEN M=0: PRINT "LEAVING ONLY 1.  ";: GOTO 150
210  PRINT "THERE ARE";M;" MATCHES LEFT IN THE PILE.": PRINT : GOTO 120
900  PRINT 
910  INPUT "  DO YOU WANT TO PLAY ANOTHER GAME (1=YES, 0=NO)?"A
920  IF A=1 THEN 100
930  PRINT : PRINT "      THANK YOU FOR THE GAME."
940  END 