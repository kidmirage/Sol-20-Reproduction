4    LET K=0
5    LET W1=0
6    PRINT "&K&J* BLKJK *&J&JTHIS DEALER GETS A BREAK AT 1945."
7    INPUT " WHAT TIME IS IT NOW---=>",X
8    FOR I=1 TO 10
9       LET X=INT(10*RND(0))
10   NEXT I
11   DIM D(52)
12   FOR A=1 TO 52
13      LET D(A)=0
14   NEXT A
15   DIM Q(52)
16   FOR A=0 TO 39 STEP 13
17      FOR C=1 TO 13
18         LET Q(A+C)=C
19      NEXT C
20   NEXT A
21   PRINT 
22   LET K=K+1
23   FOR P=1 TO 5
24      LET E(P)=0
25      LET V(P)=0
26      LET T(P)=0
27   NEXT P
28   LET V(3)=1
29   PRINT 
30   PRINT 
31   PRINT 
32   PRINT "@@@ YOUR WAGER ? ";
33   LET P=1
34   INPUT "(0 TO 500)---=>",W
35   LET W(2)=W
36   IF W<=0 THEN 240
37   IF W<=500 THEN 40
38   PRINT "THAT'S TOO MUCH - HOUSE LIMIT IS $500."
39   GOTO 32
40   PRINT 
41   PRINT "@@@   I SHOW              @@@ ";
42   GOSUB 131
43   IF E(1)=0 THEN 45
44   LET V(4)=1
45   LET V(5)=1
46   GOSUB 131
47   LET M=X
48   LET P=2
49   PRINT "(Y)  YOUR FIRST CARD IS   *** ";
50   GOSUB 131
51   LET G=X
52   PRINT "(Y)  YOUR NEXT  CARD IS   *** ";
53   GOSUB 131
54   IF V(2)>0 THEN 80
55   LET S=X
56   IF V(3)<>1 THEN 80
57   IF T(P)<>21 THEN 64
58   PRINT "***BLACKJACK***"
59   PRINT "MY HOLE CARD WAS ",
60   LET X=M
61   GOSUB 165
62   LET W1=W1+1.5*W
63   GOTO 218
64   IF V(4)=0 THEN 74
65   PRINT "INSURANCE ANYONE? (TYPE 1 OR 0)";
66   INPUT I
67   IF I=0 THEN 74
68   IF T(1)<>21 THEN 72
69   LET W1=W1+W
70   PRINT "YOU WIN $";W;" ON YOUR INSURANCE BET";
71   GOTO 74
72   LET W1=W1-W/2
73   PRINT "YOU LOST $";W/2;" ON YOUR INSURANCE BET - I DON'T HAVE BLACKJACK"
74   IF T(1)<>21 THEN 80
75   PRINT "**I HAVE BLACKJACK**"
76   PRINT "MY HOLE CARD IS ",
77   LET X=M
78   GOSUB 165
79   GOTO 216
80   IF T(P)<=21 THEN 88
81   IF E(P)>0 THEN 86
82   PRINT "&JYOU BUSTED, ";
83   PRINT "YOUR TOTAL IS  ";T(P)
84   IF V(2)=1 THEN 113
85   GOTO 195
86   LET E(P)=E(P)-1
87   LET T(P)=T(P)-10
88   IF V(1)=2 THEN 83
89   LET V(3)=V(3)+1
90   PRINT "HIT ? ";
91   INPUT "(1=HIT, 3=SPLIT, 0=NO)",V(1)
92   IF V(1)<>3 THEN 124
93   IF V(2)>0 THEN 122
94   IF V(3)<>2 THEN 122
95   IF Q(G)=Q(S) THEN 98
96   PRINT "NOW IS THAT A PAIR?"
97   GOTO 90
98   LET V(2)=1
99   IF Q(G)<>1 THEN 101
100  LET V(1)=2
101  LET P=3
102  PRINT "       PLAY HAND ONE NOW"
103  PRINT "FIRST CARD IS",
104  LET W(3)=W
105  LET X=G
106  GOSUB 108
107  GOTO 52
108  GOSUB 156
109  GOSUB 165
110  LET V(3)=1
111  LET T(P)=C
112  RETURN 
113  LET P=2
114  LET V(2)=2
115  PRINT "       PLAY HAND TWO NOW"
116  PRINT "FIRST CARD IS",
117  LET X=S
118  GOSUB 108
119  IF Q(G)=1 THEN 121
120  LET V(1)=0
121  GOTO 52
122  PRINT "NO SPLITS NOW -- TRY AGAIN"
123  GOTO 90
124  IF V(1)<>2 THEN 129
125  IF V(3)=2 THEN 128
126  PRINT "TOO LATE TO DOUBLE, CHARLIE."
127  GOTO 90
128  LET W(P)=2*W(P)
129  IF V(1)>0 THEN 52
130  GOTO 83
131  REM DEAL
132  GOSUB 139
133  LET T(P)=T(P)+C
134  IF V(5)=0 THEN 137
135  LET V(5)=0
136  RETURN 
137  GOSUB 165
138  RETURN 
139  LET N=10*(1+ABS(COS(N+W1)))
140  FOR A=1 TO N
141     LET X=INT(52.9999999*RND(0))
142     IF X=0 THEN 141
143  NEXT A
144  IF D(X)=0 THEN 154
145  LET R=R+1
146  IF R<50 THEN 139
147  FOR A=1 TO 52
148     IF D(A)=K THEN 150
149     LET D(A)=0
150  NEXT A
151  LET R=0
152  PRINT "@@@   I RESHUFFLED"
153  GOTO 139
154  LET R=0
155  LET D(X)=K
156  IF Q(X)<>1 THEN 160
157  LET C=11
158  LET E(P)=E(P)+1
159  RETURN 
160  IF Q(X)>10 THEN 163
161  LET C=Q(X)
162  RETURN 
163  LET C=10
164  RETURN 
165  REM PRINT CARD
166  GOSUB 169
167  GOSUB 183
168  RETURN 
169  IF Q(X)<>1 THEN 172
170  PRINT " ACE ";
171  RETURN 
172  IF Q(X)>10 THEN 175
173  PRINT Q(X);
174  RETURN 
175  IF Q(X)>11 THEN 178
176  PRINT " JACK ";
177  RETURN 
178  IF Q(X)>12 THEN 181
179  PRINT " QUEEN ";
180  RETURN 
181  PRINT " KING ";
182  RETURN 
183  REM PRINT SUIT
184  IF X>39 THEN 189
185  IF X>26 THEN 191
186  IF X>13 THEN 193
187  PRINT " OF SPADES"
188  RETURN 
189  PRINT " OF CLUBS"
190  RETURN 
191  PRINT " OF HEARTS"
192  RETURN 
193  PRINT " OF DIAMONDS"
194  RETURN 
195  REM DEALER PLAYS  
196  LET P=2
197  PRINT "MY HOLE CARD IS  ",
198  LET X=M
199  GOSUB 165
200  IF T(2)<22 THEN 203
201  IF V(2)=0 THEN 216
202  IF T(3)>21 THEN 216
203  LET P=1
204  IF T(1)<17 THEN 223
205  IF T(1)>17 THEN 207
206  IF E(1)>0 THEN 223
207  IF T(1)>21 THEN 226
208  LET P=2
210  IF T(P)>21 THEN 216
211  IF T(1)>21 THEN 214
212  IF T(1)>T(P) THEN 216
213  IF T(1)=T(P) THEN 217
214  LET W1=W1+W(P)
215  GOTO 217
216  LET W1=W1-W(P)
217  IF V(2)>0 THEN 220
218  GOSUB 232
219  GOTO 22
220  LET P=3
221  LET V(2)=0
222  GOTO 210
223  PRINT "I DRAW           ",
224  GOSUB 131
225  GOTO 203
226  IF E(1)=0 THEN 230
227  LET E(1)=E(1)-1
228  LET T(1)=T(1)-10
229  GOTO 203
230  PRINT "&J@@@  I BUSTED***";
231  GOTO 208
232  IF W1<0 THEN 236
233  IF W1=0 THEN 238
234  PRINT " YOU'RE AHEAD $";W1
235  RETURN 
236  PRINT "YOU'RE BEHIND $";-W1
237  RETURN 
238  PRINT "YOU'RE EVEN"
239  RETURN 
240  PRINT "&K&J&J&J"
250  IF W1=0 THEN PRINT " 0$0  YOU BROKE EVEN  0$0"
260  IF W1<0 THEN PRINT " $$$  YOU LOST $";-W1;"  $$$&J"
270  IF W1<-1000 THEN PRINT " ***** S U C K E R !!!! *****&J&J"
275  IF W1<-10000 THEN PRINT "&J ***** YOU SUPER DUMMY!!!! *****&J&J"
280  IF W1>0 THEN PRINT " ###  YOU WON $";W1;"  ###&J"
290  IF W1>1000 THEN PRINT " ***** H U S T L E R !!!! *****&J&J"
300  END 