20   SET S=8: PRINT %Z0%;TAB(17);"<<= LUNAR LANDER =>>"
30   PRINT : SET S=15: INPUT "INSTRUCTIONS (1=YES, 0=NO)?"R
50   IF R=0 THEN 210
60   SET S=3: PRINT "MISSION CONTROL CALLING LUNAR MODULE!!!"
65   FOR I=1 TO 1200: NEXT 
70   PRINT "MISSION CONTROL CALLING LUNAR MODULE!!!  DO YOU COPY???"
75   FOR I=1 TO 1200: NEXT 
80   PRINT "COMPUTER GUIDANCE FAILURE!!! MANUAL OVERRIDE NECESSARY!!!"
90   FOR N=1 TO 1200: NEXT : SET S=10
100  PRINT : PRINT "MANUAL OVERRIDE WILL ALLOW YOU TO RESET BURN RATE EVERY 10"
120  PRINT "SECONDS TO ANY RATE BETWEEN 8 AND 200 LBS/SEC."
130  PRINT "A BURN RATE OF 0 WILL RESULT IN A FREE FALL FOR 10 SECONDS."
140  PRINT "AS OF NOW YOUR STATUS IS...."
150  PRINT TAB(10);"CAPSULE WEIGHT IS 33000 LBS."
160  PRINT TAB(10);"AMOUNT OF FUEL IS 16500 LBS."
170  PRINT TAB(10);"ESTIMATED FREE FALL TIME TO IMPACT IS 120 SECS."
180  PRINT : PRINT "GOOD LUCK, YOU'LL NEED IT!!&J";
190  PRINT "MISSION CONTROL OVER AND OUT."
200  FOR I=1 TO 1500: NEXT 
210  SET S=0: PRINT 
220  PRINT "TIME        ALTITUDE        VELOCITY       FUEL       BURN RATE"
230  PRINT "SECS    MILES     FEET        MPH          LBS        LBS/SEC"
240  L=0: A=120: V=1: M=33000: N=16500
250  G=.001: Z=1.8
260  PRINT L;TAB(8);INT(A);TAB(20);5280*(A-INT(A));
270  PRINT TAB(28);3600*V;TAB(41);M-N;TAB(54);
280  INPUT ""K
290  T=10
300  IF K=0 THEN 340
310  IF K<8 THEN 330
320  IF K<=200 THEN 340
330  PRINT "ENTER A BURN RATE OF 0 OR 8 THRU 200 ONLY.";TAB(54);: GOTO 280
340  IF (M-N)<.001 THEN 450
350  IF T<.001 THEN 260
360  S=T
370  IF M>=N+S*K THEN 390
380  S=(M-N)/K
390  GOSUB 740
410  IF I<0 THEN 860
420  IF V<=0 THEN 430
425  IF J<0 THEN 910
430  GOSUB 800: GOTO 340
450  PRINT "FUEL EXHAUSTED AT";L;" SECONDS."
460  S=(-V+SQR(V*V+2*A*G))/G
470  V=V+G*S: L=L+S
490  PRINT "ON THE MOON AT";L;" SECONDS."
500  W=3600*V
510  PRINT "IMPACT VELOCITY OF";W;" MPH."
515  D2=M-N: IF D2<0 THEN D2=0
520  PRINT "FUEL REMAINING IS";D2;" LBS."
530  IF W>=60 THEN 650
540  IF W>=25 THEN PRINT "CRAFT DAMAGED!!! HOPE YOU CAN MAKE IT HOME.": GOTO 690
550  IF W>=10 THEN PRINT "CONGRATULATIONS ON A POOR LANDING!": GOTO 690
560  IF W>=1 THEN PRINT "COULD HAVE BEEN A BETTER LANDING, BUT IT WAS OK.": GOTO 690
570  PRINT "A PERFECT LANDING!!!": GOTO 690
650  PRINT "SORRY, BUT THERE WERE NO SURVIVORS."
660  PRINT "IN FACT YOU BLASTED A NEW LUNAR CRATER";W*.277777;" FEET DEEP."
670  PRINT "WELL AT LEAST THEY WILL NAME IT IN YOUR MEMORY."
690  PRINT : PRINT : PRINT 
700  INPUT "WOULD YOU LIKE TO TEMPT FATE ONCE MORE (1=YES, 0=NO)?"R
710  IF R=1 THEN 210
720  IF R<>0 THEN 700
730  PRINT : PRINT : PRINT "        MISSION CONTROL OUT.": END 
740  Q=S*K/M
742  IF Q>=.000001 THEN 750
744  Q=0
750  J=V+G*S-Z*Q*(1+Q*(.5+Q*(1/3+Q*(.25+Q/5))))
780  I=A-G*S*S/2-V*S+Z*S*Q*(.5+Q*(1/6+Q*(1/12+Q/20)))
790  RETURN 
800  L=L+S: T=T-S: M=M-S*K: A=I: V=J: RETURN 
860  IF S<.005 THEN 490
865  D=V+SQR(V*V+2*A*(G-Z*K/M))
870  S=2*A/D
880  GOSUB 740: GOSUB 800: GOTO 860
910  W=(1-M*G/(Z*K))/2
920  S=M*V/(Z*K*(W+SQR(W*W+V/Z)))+.05
930  GOSUB 800: IF I<=0 THEN 860
950  GOSUB 800
960  IF J>0 THEN 340
970  IF V>0 THEN 910
980  GOTO 340